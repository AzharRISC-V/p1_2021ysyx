## FPGA超级入门教程（主要是学习verilog）

来源：https://www.zhihu.com/column/c_1117750063287488512

0. Verilog HDL 简介

   * HDL：硬件描述语言。“描述”

   * 着重理解：

     * 阻塞（blocking）和非阻塞（non-blocking）赋值的区别
     * 顺序块和并行块的不同
     * task和function的概念等

   * Verilog HDL还有很多熊函数和任务，

     ```
     例如：$monitor, $readmemb, $stop 等
     只有通过阅读大量的verilog实例，长期实践，查阅语言手册才能逐步掌握
     ```

   * 它是一种用于数字逻辑电路设计的语言

   * 既是一种行为描述的语言，也是一种结构描述的语言。也就是，既可以是功能描述，也可以说是元器件和它们之间的连接来描述。

   * Verilog的模型，对应于5种不同级别的抽象：

     * 系统级（System）：用高级语言结构实现设计模块的外部性能的模型
     * 算法级（algorithm）：用高级语言结构实现设计算法的模型
     * RTL级（Register Transfer Level）：描述数据在寄存器之间流动和如何处理这些数据的模型
     * 门级（Gate-Level）：描述逻辑门以及逻辑门之间的连接的模型
     * 开关级（Switch-Level）：描述器件中三极管和储存节点以及它们之间连接的模型。

   * 利用Verilog HDL语言结构所提供的这种结构化和过程化的语言，其语法结构非常适合于算法级和RTL记的模型设计。这种行为描述语言具有以下功能：

     * 可描述顺序执行或并行执行的程序结构
     * 用延迟表达式或事件表达式来明确地控制过程的启动时间
     * 通过命名的事件来触发其他过程里的激活行为或停止行为
     * 提供了条件、if-else、case、循环等程序结构
     * 提供了可带参数且非零延续时间的任务（task）程序结构
     * 提供了可定义新的操作符的函数结构（function）
     * 提供了用于建立表达式的算术运算符、逻辑运算符、位运算符。

   * Verilog HDL作为一种结构化的语言也非常适合于门级和开关级的模型设计。其结构化特性包括：

     * 提供了完整的一套组合型原语（primitive）
     * 提供了双向通路和电阻器件的原语
     * 可建立MOS器件的电荷分享和电荷衰减动态模型

   * Verilog HDL的构造性语句可精确地建立信号的模型。因为它归功了延迟和输出强度的原语来建立精确程度很高的信号模型。

1. 简单的Verilog HDL模板

   * 简单的程序介绍

     * 三位加法器 adder

     ```
     module adder (cout, sum, a, b, cin);
         input [2:0] a,b;
         input cin;
         output [2:0] sum;
         output cout;
         assign {cout,sum} = a + b + cin;
     endmodule //adder
     ```

     * 比较器 compare

     ```
     module compare ( equal,a,b );
         output equal;  //声明输出信号equal
         input [1:0] a,b;  //声明输入信号a,b
         assign equal=（a==b）？1：0; 
         /*如果a、b 两个输入信号相等,输出为1。否则为0*/
     endmodule
     ```

     * 三态驱动器trist2。
       * 使用了Verilog语言库中的三态驱动器实例元件 bufif1 
       * Verilog HDL的三态门，它们的端口：一个输出、一个数据输入、一个控制输入
         * 三态门实例语句的基本语法：tristate_gate [instance_name] (OutputA, InputB, ControlC)
         * bufif0，若控制为0则数据传输至输出端，否则输出为z
         * bufif1，若控制为1则数据传输至输出端，否则输出为z
         * notif0，若控制为1则数据的非传输至输出端，否则输出为z
         * notif1，若控制为0则数据的非传输至输出端，否则输出为z

     ```
     // 三态驱动器
     module trist2 (out, in, enable);
         output out;
         input in, enable;
         bufif1 mybuf(out, in, enable);
     endmodule //trist2
     ```

     * 模块调用（例化）

     ```
     // 三态驱动器
     module trist1 (out, in, enable);
         output out;
         input in, enable;
         mytri mybuf(out, in, enable);
     endmodule //trist1
     
     // 自定义的三态门，行为类似于 bufif1
     module mytri (out, in, enable);
         output out;
         input in, enable;
         assign out = enable ? in : 'bz;
         // behavior same as bufif1
     endmodule //mytri
     ```

     * 从上面例子可以总结出Verilog HDL的一些特点：
       * Verilog HDL程序是由模块构成的。每个模块的内容都是嵌在module和endmodule两个语句之间。每个模块实现特定的功能（特别适合形式验证~~，但是嵌套以后，实例化以后呢？是树状的结构吧）。模块是可以进行层次嵌套的。于是，大型的数字电路设计可分割成不同的小模块来实现特定的功能，最后通过顶层模块调用子模块来实现整体功能。
       * 每个模块要进行端口定义，并说明输入输出口，然后对模块的功能进行行为逻辑描述。
       * 程序书写格式自耦，一行可写几个语句，也可分写多行。
       * 除了 endmodule之外，每个语句和数据定义的最后必须有分号。
       * 可用/* ... */ 和 // ... 对程序的任何部分作注释。一个好的有价值的源程序都应加上必要的注释，以增强程序可读性和可维护性。

   * 模块的结构

     * 基本设计单元"模块"（block）。一个模块有两部分组成，一部分描述接口，一部分描述逻辑功能，即定义输入是如何影响输出的（一个函数）。【模块的规范，即该函数的不变式，某些性质】

     ```
     // 一个模块， a, b 是输入， c, d 是输出
     module block (a,b,c,d);
     	input a,b;
     	output c,d;
     	
     	assign c = a | b;
     	assign d = a & b;
     endmodule
     ```

     ​	从这里看看出，每个Verilog程序包括四个主要部分：端口定义、I/O说明、内部信号声明、功能定义。

     * 模块的端口定义

       格式：

       ```
       module 模块名(port1, port2, ...);
       ```

     * 模块内容，包括：I/O说明、内部信号声明、功能定义

     * I/O说明

       格式：

       ```
       // 输入口
       input port1, port2, ..., porti;
       // 输出口
       output port1, port2, ..., portj;
       ```

     * I/O说明也可以写在端口声明语句

       格式：

       ```
       module module_name(input port1, input port2, ..., output port1, output port2,...);
       ```

     * 内部信号说明

       在模块内用到的和与端口有关的wire和reg变量的声明

       如：

       ```
       reg [width-1 : 0] R变量1, R变量2, ...;
       wire [width-1 : 0] W变量1, W变量2, ...;
       ```

     * 功能定义

       * 逻辑功能定义，有三种方法可在模块中产生逻辑

         1. 用 assign 声明语句

            ```
            assign a = b & c;	// 描述了一个有两输入的与门
            ```

         2. 用实例元件

            ```
            and and_inst(q, a, b);	// 名为 and_inst 的两输入的与门
            ```

            就像是在电路图输入下，调用库元件一样。要求每个实例元件的名字必须是唯一的。

         3. 用 always 块

            ```
            always @(posedge clk or posedge clr) begin
            	if (clr) q <= 0;
            	else if (en) q <= d;
            end
            ```

       * 说明

         * 用 assign 是描述组合逻辑最常用的方法之一。而 always 既可用于描述组合逻辑，也可用于描述时序逻辑。
         * always块有很多种描述手段来表达逻辑。例如if ... else。综合工具把源代码自动综合成用门级结构表示的组合或时序逻辑电路。

       * 注意

         * 用verilog模块实现一定的功能，首先应清楚哪些是同时发生（并行）的，哪些是顺序发生（串行）的。
         * 在always模块内，逻辑是按照指定的顺序执行。但两个或多个always模块之间是并行的。

2. 常量

   * Verilog HDL中总共有十九种数据类型，数据类型用来表示数字电路硬件中的数据储存和传送元素的。

     * 先介绍四个基本的数据类型：reg、wire、integer、parameter。
     * 其他类型：large, medium, scalared, time, small, tri, tri0, tri1, triand, trior, trireg, vectored, wand, wor。这些类型除time外斗鱼基本逻辑单元建库有关，与系统设计关系不大，无需刻意掌握。
     * 一般电路设计自动化的环境下，仿真用的基本部件库是由半导体厂家和EDA工具厂家共同提供的。系统设计工程师不必过多地关心门级和开关级的Verilog HDL语法现象。

   * 常量之数字

     1. 整数

        * 整常数有以下四种进制表示形式

          1. 二进制整数（b或B）
          2. 十进制（d或D）
          3. 十六（h或H）
          4. 八（o或O）

        * 数字表达方式有以下三种

          1. <位宽><进制><数字> 这是一种全面的描述方式
          2. <进制><数字> 数字位宽使用缺省值（由具体机器系统决定，但至少32位）
          3. <数字>，采用缺省十进制

        * 举例

          ```
          8'b10101100
          8'ha2		// A2
          ```

     2. x 和 z 

        * x表示不定值，z表示高阻值

        * **?表示高阻z**，  **?表示任意值**，一般用在casez语句中。

        * 一个x可表示：二进制的1位，八进制的3位，十进制的所有位，十六进制的4位。z表示方式与x类似。

        * 举例

          ```
          4'b10x0	// bit1不确定
          4'b101z	// bit0高阻
          12'dz	// 所有位高阻
          12'd?	// 所有位高阻
          8'h4x	// 低4位不确定
          ```

     3. 负数

        * 在位宽表达式最前面加一个减号

        * 例如

          ```
          -8'd5	// 表示5的补数
          8'd-5	// 非法格式
          ```

     4. 下划线（_ )

        * 用来分割开数的表达以提高程序可读性，只能用在具体的数字之间。

        * 举例

          ```
          16'b1010_1011_1111_1010
          8'b_0011_1010	// 非法格式
          ```

     5. 常量不说明位数时，默认值是32位，每个字母用8位的ASCII值表示

        ```
        10 = 32'd10 = 32'b1010
        1 = 32'd1 = 32'b1
        -1 = -32'd1 = 32'hFFFFFFFF
        'BX = 32'BX = 32'BXXXX_XXXX
        "AB" = 16'B01000001_01000010
        ```

   *  参数（Parameter）

     * 用parameter来定义一个标识符表示一个常量，称为符号常量。可提高程序的可读性和可维护性。

     * 格式：

       ```
       parameter param1=exp1, param2=exp2, ...;
       ```

       其中，每个exp都必须是一个常数表达式

     * 举例

       ```
       parameter msb = 7;		// 定义msb
       parameter e = 25, f = 29;	// 定义两个常数
       parameter r = 5.7;		// 实型参数
       parameter byte_size = 8, byte_msb = byte_size - 1;	// 用常数表达式来定义常数
       parameter average_delay = (r + f) / 2;
       ```

     * 常用于定义延迟时间和变量宽度。在模块或实例引用时可通过参数传递来改变参数

     * 一个例子。在构造时给参数赋值

       ```
       module Decode(A,F);
       	parameter Width = 1, Polarity = 1;
       	...
       endmodule
       
       module Top;
       	wire [3:0] A4;
       	wire [4:0] A5;
       	wire [15:0] F16;
       	wire [31:0] F32;
       	// 可以改变 Width/Polarity的值
       	Decode #(4,0) D1(A4,F16);
       	Decode #(5) D2(A5,F32);
       endmodule
       ```

     * 另一个例子。在一个模块中改变另一个模块的参数，这样的话可以起到动态改变参数的目的。

       ```
       module Test;
       	wire W;
       	Top T();
       endmodule
       
       module Top;
       	wire W;
       	Block B1();
       	Block B2();
       endmodule
       
       module Block;
       	parameter P = 0;
       endmodule
       
       // 一旦使用了该模块，则其中定义的参数修改会起作用。
       module Annotate;
       	defparam
       	Test.T.B1.P = 2,
       	Test.T.B2.P = 3;
       endmodule
       
       // 不过，这种方式，可能代码维护会有点麻烦。因为参数可能会在不同地方被改掉。
       ```

3. 变量

   * 在程序运行过程中其值可以改变的量。有很多种数据类型，这里介绍几种。

     * 网络数据类型表示结构实体（如门）之间的物理连接。网络类型的变量不能被储存值，而且必须受到驱动器（例如门、或连续赋值语句assign）的驱动。
     * 如果没有驱动器连接到网络类型的变量上，则该变量是z。
     * 常用的网络数据类型是 wire 和 tri。它们都用于连接器件单元，它们具有相同的语法格式和功能。
     * wire型用来表示单个门驱动或连续赋值语句驱动；tri表示多驱动器驱动的网络型数据。
     * 如果wire或tri变量没有定义逻辑强度（logic strength），在多驱动源的情况下，逻辑值会发生冲突从而产生不确定值。真值表如下：
       * x + * = x
       * 0 + 0 = 0,  1 + 1 = 1,  z + z = z
       * 0 + 1 = x,  0 + z = 0
       * 1 + z = 1

   * wire型

     * 常用来表示以assign关键字指定的组合逻辑信号。

     * 输入输出信号类型缺省时自动定义为wire型。

     * wire可用作任何方程式的输入，也可用作assign语句或实例元件的输出

     * 格式：

       ```
       // i个总线，每个是n位宽，即每个是n条线路
       wire [n-1:0] name1, name2, ..., namei;
       // 或者
       wire [n:1] name1, name2, ..., namei;
       // 上面两者是等价的
       ```

     * 示例

       ```
       wire a;				// 定义了一个1位的wire型数据
       wire [7:0] b;		// 定义了一个8位的wire型数据
       wire [4:1] c, d;	// 定义了两个4位的wire型数据
       ```

   * reg型

     * 寄存器是数据储存单元的抽象。

     * 通过赋值语句可以改变寄存器储存的值，其作用与改变触发器储存的值相当

     * reg类型数据的缺省值为不确定x

     * reg型数据常用来表示用于always模块内的指定信号，常代表触发器。在设计中要由“always”块通过使用行为描述语句来表达逻辑关系。在“always”块内被赋值的每一个信号都必须定义成reg型。

     * 格式：

       ```
       reg [n-1:0] name1, name2, ..., namei;
       // 或
       reg [n:1] name1, name2, ..., namei;
       ```

     * 实例

       ```
       reg rega;				// 定义了一个1位的reg型数据
       reg [3:0] regb;			// 定义了一个4位的reg型数据
       reg [4:1] regc,regd;	// 定义了两个4位的reg型数据
       ```

   * wire和reg用法的总结

     * wire用法总结
       1. wire可在Verilog中表示任意宽度的单线/总线
       2. **wire可用于模块的输入和输出端口**以及一些其他元素在实际模块声明中
       3. wire不能存储值（无状态），不能在always@块内赋值（=或<=）左侧使用
       4. wire是assign语句左侧唯一的合法类型
       5. wire只能用于组合逻辑
     * reg用法总结
       1. 类似于wire，但可以存储信息（有内存，有状态），允许连接到模块的输入端口，但不能连接到实例化的输出
       2. **在模块声明中，reg可用作输出，但不能用作输入**
       3. 在always@(...)块内，=或<=赋值语句的左边必须是reg类型变量
       4. 在initial语句块内，=赋值语句的左侧必须是reg变量
       5. reg不能用于assign赋值语句的左侧
       6. 当与@(posedge clk)块一起使用时，reg可用于创建寄存器
       7. reg可用于组合逻辑和时序逻辑
     * 构建module时
       * input必须是wire
       * output可以是wire，也可以是reg
       * inout必须是wire
     * 例化模块时
       * 外部链接input端口的可以是wire，也可以是reg
       * 外部链接output端口的必须是wire
       * 外部链接inout端口的必须是wire

   * memory型

     * 通过对reg型变量建立数组来对存储器建模，可以描述RAM型存储器，ROM存储器和reg文件。数组中的每一个单元通过一个数组索引进行寻址。在Verilog中没有多维数组存在。memory型数据是通过扩展reg型数据的地址范围来生成的。格式为：

       ```
       reg [n-1:0] 存储器名[m-1:0];
       // 其中，reg [n-1:0]定义了每个存储单元的大小，[m-1:0]定义了该存储器中有多少个这样的存储器。
       reg [7:0] mema[255:0];		// 定义了256个8位的存储器
       // 又比如：
       parameter wordsize = 16;
       parameter memsize = 256;
       reg [wordsize-1:0] mem[memsize-1:0];
       ```

     * 注意，n个1位寄存器构成的是存储器组，而1个n位的寄存器，这两者是不同的。

       ```
       reg [n-1:0] rega;		// 一个n位的寄存器
       reg mema [n-1:0];		// n个1位的存储器组
       // 1个n位的寄存器可在一条赋值语句里进行赋值，而一个存储器则不行
       rega = 0;		// 合法
       mema = 0;		// 非法
       // 想要对存储器单元进行读写，必修指定该单元在存储器中的地址
       mema[3] = 0;	// 合法
       ```

4. 运算符

   * Verilog HDL语言的运算符范围很广，按功能可分为以下几类：

     ```
     1. 算术运算符(+, -, *, /, %)
     2. 赋值运算符(=, <=)
     3. 关系运算符(>, <, >=, <=)
     4. 逻辑运算符(&&, ||, !)
     5. 条件运算符( ? : )
     6. 位运算符(~, &, |, ^, ^~)		// 按位取反，按位与，按位或，按位异或，按位同或
     7. 移位运算符(<<, >>)			// a << n。 逻辑移位，都用0来填补
     8. 拼接运算符({})
     9. 其他
     10. 等式运算符（==, !=, ===, !==)
     ```

   * 实例

     ```
     clk = ~clk;		// 按位取反
     c = a | b;		// 按位或
     r = s ? t : u;	// 条件运算
     ```

   * 补充说明

     * 按位运算

       * 如果含有x，则要注意
       * 不同长度的数据，系统自动将两者按右端对齐，位数少的会在高位填充0

     * 逻辑运算

     * 关系运算

       * 假则返回0，真则返回1，若某操作数值不确定则关系模糊，返回值也不定。

     * 等式运算符

       * == 和 != 称为逻辑等式运算符。操作数中某些位如果是不定值x或高阻值z，结果为不定值x。== 的真值表：

         | ==   | 0    | 1    | x    | z    |
         | ---- | ---- | ---- | ---- | ---- |
         | 0    | 1    | 0    | x    | x    |
         | 1    | 0    | 1    | x    | x    |
         | x    | x    | x    | x    | x    |
         | z    | x    | x    | x    | x    |

       * === 和 !== 称为case等式运算符。操作数中某些位如果是x或z，结果为0。===的真值表：

         | ===  | 0    | 1    | x    | z    |
         | ---- | ---- | ---- | ---- | ---- |
         | 0    | 1    | 0    | 0    | 0    |
         | 1    | 0    | 1    | 0    | 0    |
         | x    | 0    | 0    | 1    | 0    |
         | z    | 0    | 0    | 0    | 1    |

     * 移位操作

       ```
       // 都是逻辑移位，用0来填补空位。但注意左移后改变变量的位数。
       4'b1001<<1 = 5'b10010
       4'b1001<<2 = 6'b100100
       1<<6 = 32'b1000000
       4'b1001>>1 = 4'b0100
       4'b1001>>4 = 4'b0000
       ```

     * 位拼接运算符（concatation）

       ```
       // 把多个信号的某些为拼接起来
       // 格式：
       {信号1的某几位, 信号2的某几位, ..., 信号n的某几位}
       // 例如：
       {a, b[3:0], w, 3'b101}
       // 也可写成
       {a, b[3], b[2], b[1], b[0], w, 1'b1, 1'b0, 1'b1}
       // 在位拼接表达式中必须指明信号的位数
       
       // 可用重复法来简化表达式
       {4{w}}			// 等同于{w,w,w,w}
       // 还可用嵌套的方式来表达
       {b,{3{a,b}}}	// 等同于{b,a,b,a,b,a,b}
       ```

     * ，缩减运算符（对单个操作数进行或与非递推运算，最后的结果是一位的二进制数）

       ```
       // 具体运算过程：1/2位或与非，再与3位..., 以此类推，直至最后一位
       // 例如：
       reg [3:0] B;
       reg C;
       C = &B;	// 表示： ((B[0] & B[1]) & B[2]) & B[3];
       ```

   * 优先级别

     优先级从高到低

     ```
     ! ~
     *  /  %
     +  -
     <<  >>
     <  <=  >  >=
     ==  !=  ===  !==
     &
     ^  ^~
     |
     &&
     ||
     ?:
     ```

     优先级不需要记忆，因为：

     * 极少在一条语句用多个操作符。如果用了，说明逻辑写的不够好
     * 如果有，加括号更保险

   

5. 标识符与关键字

   * 标识符

     * 用户在描述时给Verilog对象起的名字。比如模块名、端口名、例化的基本门元件名等
     * 标识符必须以字母（a-z  A-Z）或（_)开头，后面是字母、数字、$ 或 _ 。

   * 关键字

     * 事先定义好的确认符，用来组织语言结构。小写字母。

     * 关键字列表（无需特意去记忆）

       ```
       always, and, assign, begin, buf, bufif0, bufif1, case, casex, casez, cmos, deassign, default, defparam, disable, edge, else, end, endcase, endmodule, endfunction, endprimitive, endspecify, endtable, endtask, event, for, force, forever, fork, function, highz0, highz1, if, initial, inout, input, integer, join, large, macromodule, medium, module, nand, negedge, nmos, nor, not, notif0, notif1, or, output, parameter, pmos, posedge, primitive, pull0, pull1, pullup, pulldown, rcmos, reg, releases, repeat, mmos, rpmos, rtran, rtranif0, rtanif1, scalared, small, specify, specparam, strength, strong0, strong1, supply0, supply1, table, task, time, tran, tranif0, tranif1, tri, tri0, tri1, triand, trior, trireg, vectored, wait, wand, weak0, weak1, while, wire, wor, xnor, xor
       ```

6. 阻塞赋值与非阻塞赋值

   * 初步理解阻塞赋值与非阻塞赋值

     * 两种赋值方式

       * 非阻塞（Non_Blocking）赋值方式（如 b <= a;)
         * 块结束后才完成赋值操作
         * b的值不是立即就改变
         * 常用的赋值方法。（特别是在编写可综合模块时）
       * 阻塞（Blocking）赋值方式（如 b = a;)
         * 赋值语句执行完后，块才结束。
         * b的值在赋值语句执行完后立刻就改变
         * 可能产生意想不到的结果

     * 这两者的区别常给设计人员带来问题。问题主要是给“always”块内的reg型信号的赋值方式不容易把握。

     * 一个例子

       ```
       always @(posedge clk) begin
       	b <= a;
       	c <= b;
       end
       // 使用了非阻塞赋值方式，定义了两个reg型信号b和c，clk上升沿到来时，b就等于a，c就等于b（等于上个周期的a，不是现在的a）
       // 这里应该用到了两个触发器。
       // 赋值是在“always”块结束后执行的，c应该是原来b的值，并不会立即等于a的值，要到下一个周期才会等于刚才a的值。
       // 电路结构：
       // 1. 两个触发器D1,D2，clk同时连接它们的时钟输入信号
       // 2. D1的输入是a,输出是b
       // 3. D2的输入是b，输出是c
       ```

     * 一个例子

       ```
       always @(posedge clk) begin
       	b = a;
       	c = b;
       end
       // 使用了阻塞赋值方式。clk上升沿到来时，b马上取a的值，c马上取b的值（等于a）
       // 电路结构
       // 1. 一个触发器D1，clk接时钟输入信号
       // 2. D1的输入是a，输出是b和c。
       ```

   * 深入理解阻塞和非阻塞赋值的不同

     * 在描述组合逻辑的always块中用**阻塞赋值**，则综合成组合逻辑的电路结构。

     * 在描述时序逻辑的always块中用非阻塞赋值，则综合成时序逻辑的电路结构。

     * 理解两个定义：

       * RHS：Right Hand Side，右侧的表达式或变量
       * LHS：Left。。。

     * 阻塞赋值

       * 一个步骤：计算RHS并更新LHS。

       * 阻塞是指在同一个always块中，其后面的赋值语句是在前一个赋值语句结束后再开始执行。

       * 一个不好的例子。如果一个过程块中阻塞赋值的RHS变量正好是另一个过程块中阻塞赋值的LHS变量，这两个过程块又用同一个时钟沿出发，则执行的次序是无法确定的。

         ```
         // 用阻塞赋值的反馈振荡器（不好的例子，出现不稳定的冒险和竞争）
         module fbosc1 (y1, y2, clk, rst);
          output y1, y2; 
          input clk, rst;
          reg y1, y2;
         
          always @(posedge clk or posedge rst)
          begin
              if (rst) y1 = 0; // reset
              else y1 = y2;
          end
         
          always @(posedge clk or posedge rst)
          begin
              if (rst) y2 = 1; // preset
              else y2 = y1;
          end
         endmodule
         // 分析：
         // 若第一个always块的rst信号先到0时刻，则y1和y2都会取1（执行了y1=y2，而y2=1）
         // 若第二个always块的rst信号先到0时刻，则y1和y2都会去0（执行了y2=y1，而y1=0)
         // 这清楚的说明这个verilog模块是不稳定的，会产生冒险和竞争的情况
         ```

     * 非阻塞赋值

       * 两个步骤：在赋值时刻开始时，计算非阻塞赋值的RHS表达式；在赋值时刻结束时，更新非阻塞赋值LHS表达式。

       * 例子。

         ```
         // 用非阻塞赋值的反馈振荡器（正确示范）
         module fbosc2 (y1, y2, clk, rst);
          output y1, y2;
          input clk, rst;
          reg y1, y2;
          
          always @(posedge clk or posedge rst)
          begin
              if (rst) y1 <= 0; // reset
              else y1 <= y2;
          end
         
          always @(posedge clk or posedge rst)
          begin
              if (rst) y2 <= 1; // preset
              else y2 <= y1;
          end
         endmodule
         // 分析：
         // 1. 无论哪个always块中的rst信号到达0，这两个always块中的阻塞赋值都在赋值开始时刻计算RHS表达式，而在结束时刻才更新LHS表达式。
         // 2. 所以这两个always块在复位信号到来后，在always块结束时，y1为0而y2为1是确定的。
         // 3. 从用户的角度看这两个非阻塞赋值时并行执行的。
         ```

       * 非阻塞赋值操作只能用于对计算器类型变量进行赋值，只能用在 initial 和 always 等过程块中。

       * 非阻塞赋值不允许连续赋值。

   * 通过移位寄存器的例子来理解阻塞赋值和非阻塞赋值

     * 硬件结构【移位寄存器】

       * 三个触发器D1,D2,D3，时钟信号都是clk
       * D1的输入是d，输出是q1
       * D2的输入是q1，输出是q2
       * D3的输入是q2，输出是q3

     * 错误方式#1

       ```
       // 用阻塞赋值来描述移位寄存器，不正确的例子。b : blocking
       module pipeb1(q3, d, clk);
       	output [7:0] q3;
       	input [7:0] d;
       	input clk;
       	reg [7:0] q3, q2, q1;
       	
       	always @(posedge clk) begin
       		q1 = d;
       		q2 = q1;
       		q3 = q2;
       	end
       endmodule
       // 分析：
       // 顺序进行阻塞赋值，所有的寄存器输出值都等于输入值d。
       // 即，每个clk上升沿，输入值d将无延时地直接输出到q3
       ```

     * 错误方式#2

       ```
       // 用阻塞赋值来描述移位寄存器，可行，但不好的风格。
       module pipeb2(q3, d, clk);
       	output [7:0] q3;
       	input [7:0] d;
       	input clk;
       	reg [7:0] q3, q2, q1;
       	
       	always @(posedge clk) begin
       		q3 = q2;
       		q2 = q1;
       		q1 = d;
       	end
       endmodule
       // 分析：
       // 阻塞赋值的次序是经过仔细安排的。
       // 虽然该模块可被综合成移位寄存器，但不建议用这种风格来描述时序逻辑。
       ```

     * 错误方式#3

       ```
       // 用阻塞赋值来描述移位寄存器，可行，但不好的风格。
       module pipeb3(q3, d, clk);
       	output [7:0] q3;
       	input [7:0] d;
       	input clk;
       	reg [7:0] q3, q2, q1;
       	
       	always @(posedge clk) q1 = d;
       	always @(posedge clk) q2 = q1;
       	always @(posedge clk) q3 = q2;
       endmodule
       // 分析：
       // 阻塞赋值被放在不同的always块里。
       // 仿真时，这些块的先后熟悉是随机的，因此可能出现错误的结果。这是Verilog的竞争冒险，按不同的顺序执行这些块将导致不同的结果。
       // 但是，这些代码的综合结果却是正确的流水线寄存器。
       // 换言之，前仿真和后仿真结果可能会不一致。
       ```

     * 错误方式#4

       ```
       // 用阻塞赋值来描述移位寄存器，可行，但不好的风格。
       module pipeb4(q3, d, clk);
       	output [7:0] q3;
       	input [7:0] d;
       	input clk;
       	reg [7:0] q3, q2, q1;
       	
       	always @(posedge clk) q2 = q1;
       	always @(posedge clk) q3 = q2;
       	always @(posedge clk) q1 = d;
       endmodule
       // 分析：
       // 同方式#3一样，仿真时可能出错。但综合后是正确的。
       ```

     * 正确方式#1

       ```
       // 用非阻塞赋值来描述移位寄存器的设计风格
       module pipen1(q3, d, clk);
       	output [7:0] q3;
       	input [7:0] d;
       	input clk;
       	reg [7:0] q3, q2, q1;
       	
       	always @(posedge clk) begin
       		q1 <= d;
       		q2 <= q1;
       		q3 <= q2;
       	end
       endmodule
       ```

     * 正确方式#2

       ```
       // 用非阻塞赋值来描述移位寄存器的设计风格
       module pipen1(q3, d, clk);
       	output [7:0] q3;
       	input [7:0] d;
       	input clk;
       	reg [7:0] q3, q2, q1;
       	
       	always @(posedge clk) begin
       		q3 <= q2;
       		q2 <= q1;
       		q1 <= d;
       	end
       endmodule
       ```

     * 正确方式#3

       ```
       // 用非阻塞赋值来描述移位寄存器的设计风格
       module pipen1(q3, d, clk);
       	output [7:0] q3;
       	input [7:0] d;
       	input clk;
       	reg [7:0] q3, q2, q1;
       	
       	always @(posedge clk) q1 <= d;
       	always @(posedge clk) q2 <= q1;
       	always @(posedge clk) q3 <= q2;
       endmodule
       ```

     * 正确方式#4

       ```
       // 用非阻塞赋值来描述移位寄存器的设计风格
       module pipen1(q3, d, clk);
       	output [7:0] q3;
       	input [7:0] d;
       	input clk;
       	reg [7:0] q3, q2, q1;
       	
       	always @(posedge clk) q2 <= q1;
       	always @(posedge clk) q3 <= q2;
       	always @(posedge clk) q1 <= d;
       endmodule
       ```

   * 阻塞赋值和非阻塞赋值的原则归纳如下：

     * 原则1：时序电路建模时，用非阻塞赋值。
     * 原则2：锁存器电路建模时，用非阻塞赋值。
     * 原则3：用always块写组合逻辑时，采用阻塞赋值。
     * 原则4：同一个always块中同时建立时序和组合逻辑电路时，用非阻塞赋值。
     * 原则5：在同一个always块中不要同时使用非阻塞赋值和阻塞赋值。
     * 原则6：不要再多个always块中为同一个变量赋值

7. 顺序块与并行块（begin...end与 fork...join)

   * 块语句通常用来将两条或多条语句组合在一起，使其在格式上看更像一条语句。块语句有两种：

     * begin...end语句，用来标识顺序执行的语句，这种块称为顺序块
     * fork...join语句，用来标识并行执行的语句，这种块称为并行块

   * 块语句用于结构说明语句 initial语句 和 always 语句中。

     * initial begin ... end
     * always begin ... end
     * initial fork ... join
     * always fork ... join

   * 顺序块

     * 特点：

       * 块内的语句是按顺序执行的，只有上面一条语句执行完后下面的语句才能执行。
       * 每条语句的延迟时间是相对于前一条语句的仿真时间而言的。
       * 直到最后一条语句执行完，程序流程控制才跳出该语句块

     * 格式如下：

       ```
       begin
       	语句1;
       	语句2;
       	...
       	语句n;
       end
       
       // 或者
       
       begin: 块名
       	块内声明语句
       	语句1;
       	语句2;
       	...
       	语句n;
       end
       ```

       其中，块名即该块的名字，一个标识名。

       块内声明语句可以是参数声明语句，reg型、integer型、real型变量声明语句

     * 举例

       ```
       // 例1
       begin
       	areg = breg;
       	creg = areg;
       end
       
       // 例2，使用延迟控制时间
       begin
       	areg = breg;
       	#10 creg = areg;	// 延迟10个时间单位
       end
       
       // 例3，一个时序波形
       parameter d = 50;
       reg [7:0] r;	// 一个8位寄存器变量
       begin
       	#d r = 'h35;
       	#d r = 'hE2;
       	#d r = 'h00;
       	#d r = 'hF7;
       	#d -> end_wave;		// 触发事件 end_wave
       end
       ```

   * 并行快

     * 特点：

       * 块内语句同时执行。
       * 块内每条语句的延迟时间是相对于程序流程控制进入到块内时的仿真时间
       * 延迟时间是用来给赋值语句提供执行时序的
       * 当按时间时序排序在最后的语句执行完后或一个disable语句执行时，程序流程控制跳出该程序块。

     * fork和join语句常用于test bench描述中。因为可以一起给出矢量及其绝对时间

     * 格式：

       ```
       fork
       	语句1;
       	语句2;
       	...
       	语句n;
       join
       
       // 或者
       
       fork: 块名
       	块内声明语句
       	语句1;
       	语句2;
       	...
       	语句n;
       join	
       ```

       其中，块名即该块的名字，一个标识名。

       块内声明语句可以是参数声明语句，reg型、integer型、real型、time型变量声明语句，以及event（事件）型说明语句。

     * 举例

       ```
       fork
       	#50 r = 'h35;
       	#100 r = 'hE2;
       	#150 r = 'h00;
       	#200 r = 'hF7;
       	#250 -> end_wave;	// 触发事件 end_wave
       join
       ```

       此例子与之前顺序块中的例子作用相同。

   * 块名

     * 给块取名，只需在begin或fork后面加上名字即可。这样做的原因如下：
       * 可以在块内定义局部变量，只在块内使用的变量。
       * 可允许被其他语句调用，如被disable语句
       * 在Verilog语言里，所有变量都是静态的，即所有变量都只有一个唯一的存储地址，因此进入或跳出块并不影响存储在变量内的值。
     * 基于以上原因，块名就提供了一个在任何仿真时刻确认变量值的方法。（个人理解，使用块名才能访问到块内的变量及其值，而且必须这样做，因为所有变量都是静态的，都有地址可以访问）

   * 起始时间和结束时间

     * 对于顺序块，起始时间是第一条语句开始被执行的时间，结束时间是最后一条语句执行完的时间。

     * 对于并行块，起始时间对于块内所有的语句是相同的，即程序流程控制进入该块的时间，结束时间是按时间排序在最后的语句执行完的时间。

     * 在fork_join块内，各条语句不必按顺序给出，因为是在并行块内。

     * 例如，如下块与上面的fork_join块

       ```
       fork
       	#200 r = 'hF7;
       	#150 r = 'h00;
       	#50 r = 'h35;
       	#250 -> end_wave;	// 触发事件 end_wave
       	#100 r = 'hE2;
       join
       ```

   * 注意：fork-join是典型的不可综合语句，并且在一些仿真器时效率较差。

   * 补充知识：可综合与不可综合

     ```
     // Verilog中可综合与不可综合的语句
     (1) 所有综合工具都支持的结构
     always, assign, begin, end, case, wire, tri, supply0, supply1, reg, integer, default, for, function, and, nand, or, nor, xor, xnor, buf, not, bufif0, bufif1, notif0, notif1, if, inout, input, instantitation, module, negedge, posedge, operators, output, parameter
     (2) 所有综合工具都不支持的结构
     time, defparam, $finish, fork, join, initial, delays, UDP, wait
     (3) 有些工具支持有些工具不支持的结构
     casex, casez, wand, triand, wor, trior, real, disable, forever, arrays, memories, repeat, task, while
     
     // 建立可综合模型的原则
     1. 不使用initial
     2. 不使用 #10
     3. 不使用循环次数不确定的循环语句，如forever, while等 
     4. 不使用用户自定义原语（UDP元件）
     5. 尽量使用同步方式设计电路
     6. 除非是关键路径的设计，一般不采用调用门级元件描述设计的方法，建议采用行为语句来完成设计。
     7. 用always过程块描述组合逻辑，应在敏感信号列表中列出所有的输入信号
     8. 所有的内部寄存器都应该能被复位，在使用FPGA实现设计时，尽量使用器件的全局复位端作为系统总的复位。
     9. 对时序逻辑描述和建模，尽量使用非阻塞赋值方式。对组合逻辑描述和建模，既可以使用阻塞赋值，也可以使用非阻塞赋值。但在同一个过程块中，最好不要同时用阻塞赋值和非阻塞赋值
     10. 不能在多个always块中对同一个变量赋值。
     11. 如果不打算把变量推导成锁存器，则必须在if语句的所有条件分支中都对变量明确地赋值。
     12. 避免混合使用上升沿和下降沿触发的触发器。
     13. 同一个变量的赋值不能受多个时钟控制，也不能受两种不同的时钟条件（或不同的时钟沿）控制。
     14. 避免在case语句的分支中使用x值或z值。
     ```

     

8. 条件语句

   * if_else语句。有三种形式的if语句

     ```
     // (1). if (cond) exp1
     if (a > b)
     	out1 <= in1;
     
     // (2). if (cond) exp1 else exp2
     if (a > b)
     	out1 <= in1;
     else
     	out1 <= in2;
     
     // (3) 多个 if ... else 嵌套
     if (a > b)
     	out1 <= in1;
     else if (a == b)
     	out1 <= in2;
     else 
     	out1 <= in3;
     ```

     * 一些说明

       1. 条件表达式，若为0,x,z，按false处理；若为1，按true处理。

       2. 允许一定形式的表达式简写方式

          ```
          if (exp) 等同于 if (exp == 1)
          if (!exp) 等同于 if (exp != 1)
          ```

       3. if语句的嵌套

          ```
          if (cond1)
          	if (cond2) exp1
              else exp2
          else
          	if (cond3) exp3
          	else exp4
          ```

       4. else匹配遵循最近原则

          ```
          if (index > 0) begin
          	for (scani = 0; scani < index; scani = scani + 1)
          		if (mem[scani] > 0)
          			begin
          				$display("...");
          				mem[scani] = 0;
          			end
          else // 错误，这个else 是与内部的 if 匹配
          	$display("error-indexiszero");
          ```

   * case语句

     * case语句可处理多分支选择。一般形式为：

       ```
       case (cond) <case分支项> endcase
       或
       casez (cond) <case分支项> endcase
       或
       casex (cond) <case分支项> endcase
       ```

       case分支项的一般格式：

       ```
       分支表达式:			语句
       缺省项(default项):	 语句
       ```

     * 说明

       * default项可有可无，一个case语句只准有一个default项。
       * casez用来处理不关心高阻值z的比较情况，casex则处理不关心z和x的情况。不关心是指，在表达式进行比较时，不将该位的状态考虑在内。
       * 

     * 一个例子

       ```
       // 对寄存器 rega 译码以确定 result 的值
       reg [15:0] rega;
       reg [9:0] result;
       case (rega)
       	16'd0: result = 10'b0111111111;
       	16'd1: result = 10'b1011111111;
       	16'd2: result = 10'b1101111111;
       	16'd3: result = 10'b1110111111;
       	16'd4: result = 10'b1111011111;
       	16'd5: result = 10'b1111101111;
       	16'd6: result = 10'b1111110111;
       	16'd7: result = 10'b1111111011;
       	16'd8: result = 10'b1111111101;
       	16'd9: result = 10'b1111111110;
       	default: result = 'bx;
       endcase
       ```

     * case的真值表

       | case |  0   |  1   |  x   |  z   |
       | :--: | :--: | :--: | :--: | :--: |
       |  0   |  1   |  0   |  0   |  0   |
       |  1   |  0   |  1   |  0   |  0   |
       |  x   |  0   |  0   |  1   |  0   |
       |  z   |  0   |  0   |  0   |  1   |

     * casez的真值表

       | casez |  0   |  1   |  x   |  z   |
       | :---: | :--: | :--: | :--: | :--: |
       |   0   |  1   |  0   |  0   |  1   |
       |   1   |  0   |  1   |  0   |  1   |
       |   x   |  0   |  0   |  1   |  1   |
       |   z   |  1   |  1   |  1   |  1   |

     * casex的真值表

       | casex |  0   |  1   |  x   |  z   |
       | :---: | :--: | :--: | :--: | :--: |
       |   0   |  1   |  0   |  1   |  1   |
       |   1   |  0   |  1   |  1   |  1   |
       |   x   |  1   |  1   |  1   |  1   |
       |   z   |  1   |  1   |  1   |  1   |

     * 含有x,z语句的例子

       ```
       // 例1
       case (select[1:2])
       	2'b00: result = 0;
       	2'b01: result = flaga;
       	2'b0x,
       	2'b0z: result = flaga ? 'bx : 0;
       	2'b10: result = flagb;
       	2'bx0,
       	2'bz0: result = flagb ? 'bx : 0;
       	default: result = 'bx;
       endcase
       
       // 例2
       reg [7:0] ir;
       casez(ir)
       	8'b1???????: instruction1(ir);
       	8'b01??????: instruction2(ir);
       	8'b00010???: instruction3(ir);
       	8'b000001??: instruction4(ir);
       endcase
       
       // 例3
       reg [7:0] r, mask;
       mask = 8'bx0x0x0x0;
       case (r^mask)
       	8'b001100xx: stat1;
       	8'b1100xx00: stat2;
       	8'b00xx0011: stat3;
       	8'bxx001100: stat4;
       endcase
       ```

   * 如何避免生成不希望的锁存器的错误

     * if语句，最好写上else项
     * case语句，最好写上default项

   

9. 循环语句

   * 四种类型的循环语句，用来控制执行语句的执行次数。

     * forever连续的执行语句
     * repeat执行n次
     * while执行到某个条件不满足。若一开始条件不满足，则一次也不执行
     * for与C语言类似。执行以下三个步骤来决定语句的循环次数
       * a) 先给控制循环次数的变量赋初值
       * b) 判断控制循环的表达式，如果为假则跳出循环语句，如果为真则执行指定的语句后，转到第三步
       * c) 执行修改控制循环变量的语句，然后返回第二步。

   * forever语句

     * 格式

       ```
       forever 语句;
       // 或
       forever begin
       	多条语句
       end
       ```

     * 常用于产生周期性的波形，用来作为仿真测试信号。它与always语句不同之处在于不能独立写在程序中，而必须写在initial块中。

   * repeat语句

     * 格式

       ```
       repeat (次数表达式) 语句;
       ```

     * 举例

       ```
       // 使用repeat循环语句及加法和移位操作来实现一个乘法器
       parameter size = 8, longsize = 16;
       reg [size:1] opa, opb;
       reg [longsize:1] result;
       
       begin: mult
       	reg [longsize:1] shift_opa, shift_opb;
       	shift_opa = opa;
       	shift_opb = opb;
       	result = 0;
       	
       	repeat (size) begin
       		if (shift_opb[1])
       			result = result + shift_opa;
       		shift_opa = shift_opa << 1;
       		shift_opb = shift_opb >> 1;
       	end
       end
       ```

   * while语句

     * 格式

       ```
       while (条件表达式) 语句
       ```

     * 举例

       ```
       // 对rega这个8位的二进制数中值为1的位进行计数
       begin: count1s
       	reg [7:0] tempreg;
       	count = 0;
       	tempreg = rega;
       	
       	while (tempreg) begin
       		if (tempreg[0])
       			count = count + 1;
       		tempreg = tempreg >> 1;
       	end
       end
       ```

   * for语句

     * 格式

       ```
       for (循环变量赋初值; 循环结束条件; 循环变量增值) 执行语句;
       
       // 相当于
       begin
       	循环变量赋初值;
       	while (循环结束条件)
       	begin
       		执行语句;
       		循环变量增值;
       	end
       end
       ```

     * 注意，四种循环语句中，综合工具只支持for循环语句

     * 举例

       ```
       // 例1，初始化memory
       begin: init_mem
       	reg [7:0] tempi;
       	
       	for (tempi = 0; tempi < memsize; tempi = tempi + 1)
       		memory[tempi] = 0;
       end
       
       // 例2，类似于前面repeat语句实现的乘法器
       parameter size = 8, longsize = 16;
       reg [size:1] opa, opb;
       reg [longsize:1] result;
       
       begin: mult
       	integer bindex;
       	result = 0;
       	for (bindex = 1; bindex <= size; bindex = bindex + 1)
       		if (opb[bindex])
       			result = result + (opa << (bindex - 1));
       end
       
       // 例3，对rega这个8位二进制计数器中值为1的位进行计数
       begin: count1s
       	reg [7:0] tempreg;
       	count = 0;
       	for (tempreg = rega; tempreg; tempreg = tempreg >> 1)
       		if (tempreg[0])
       			count = count + 1;
       end
       ```

     

10. 过程块（initial和always）

    * 过程块是行为模型的基础。过程块有两种：initial块，只执行一次；always块，循环执行。

    * 过程块中有下列部件

      * 过程赋值语句：在描述过程块中的数据流
      * 高级结构（循环、条件语句）：描述块的功能
      * 时序控制：控制块的执行及块中的语句

    * initial语句

      * initial块常用于测试文件的虚拟模块的编写，用来产生仿真测试信号和设置信号记录等仿真环境

    * always语句

      * 格式：

        ```
        always <时序控制> <语句>
        
        // 例。生成周期为20的无限延续的信号波形
        always #10 areg = ~areg;
        
        // 时间控制，可以是沿触发，也可以是电平触发。也可以是多个信号，中间用关键字or连接。
        always @(posedge clk or posedge rst) begin
        	...
        end
        always @(a or b or c) begin
        	...
        end
        ```

      * always @()用法如下

        ```
        always @(clk)			// 信号有变化就触发事件
        always @(posedge clk)	// 信号有上升沿就触发事件
        always @(negedge clk)	// 信号有下降沿就触发事件
        always @(事件1 or 事件2 or ...)		// 敏感事件之一就触发事件
        always @(*)				// 无敏感列表，描述组合逻辑，和assign语句是有区别的。
        
        // 区分：
        always @(*) b = 1'b0;
        // 与
        assign b = 1'b0;
        // 1. 被assign赋值的信号定义为wire型，被 always @(*)结构块下的信号定义为 reg 型。
        // 2. assign是硬连线。而always @(*)表示块内的任意信号变化时触发。
        ```

      

11. 任务（task）和函数（function）

    * 简介

      * 利用任务和函数可以把一个很大的程序模块分解成许多较小的任何和函数便于理解和调试。
      * 输入、输出和总线信号的值可以传入、传出任务和函数。任务和函数往往还是大的程序模块中在不同地点多次用到的相同的程序段。
      * 使用task和function语句可以简化程序的结构，是编写较大型模块的基本功。

    * task和function说明语句的不同点

      * 主要的不同点。
        * 函数只能与主模块共用同一个仿真时间单位，而任务可以定义自己的仿真时间单位
        * 函数不能启动任务，而任务能启动其他任务和函数
        * 函数至少要有一个输入变量，而任务可以没有或有多个任何类型的变量
        * 函数返回一个值，而任务则不返回值。
      * 函数的目的，通过返回一个值来响应输入信号的值。
      * 任务能支持多种目的，能计算多个结果值，这些结果只能通过被调用的任务的输出或总线端口送出。
      * 使用函数时是把它当作表达式中的操作符，该操作的结果就是这个函数的返回值。

    * task说明语句

      * 如果传给任务的变量值和任务完成后接收结果的变量已定义，就可以用一条语句启动任务。任务完成以后控制就传回启动过程。如任务内部有定时控制，则启动的时间可以与控制返回的时间不同。任务可以启动其他的任务，可以嵌套的启动，可启动的任务数没有限制。只有当所有的启动任务完成以后，控制才能返回。

      * 任务的定义

        * 语法：

          ```
          task <任务名>;
          	<端口即数据类型声明语句>
          	<语句1>
          	<语句2>
          	...
          	<语句n>
          endtask
          ```

        * 任务的调用及变量的传递

          * 任务调用的语法

            ```
            <任务名>(端口1, 端口2, ..., 端口n);
            ```

          * 示例

            ```
            // 任务定义
            task my_task;
            	input a,b;
            	inout c;
            	output d,e;
            	<执行任务工作的语句>
            	...
            	c = foo1;
            	d = foo2;
            	e = foo3;
            endtask
            // 任务调用
            my_task(v,w,x,y,z);
            ```

            说明，当任务启动时，由v,w和x传入的变量赋值给a,b和c，当任务完成后的输出又通过c,d和e赋给x,y和z。

          * 一个具体的例子

            ```
            modue traffic_lights;
            	reg clk,red,amber,green;
            	parameter on=1, off=0, red_ticks=350,
            		amber_ticks=30, green_ticks=200;
            	// 交通灯初始化
            	initial red=off;
            	initial amber=off;
            	initial green=off;
            	// 交通灯控制时序
            	always begin
            		red=on;
            		light(red,red_tics);	// 调用等待任务
            		green=on;
            		light(green,green_tics);
            		amber=on;	// 开黄灯
            		light(amber,amber_tics);
            	end
            	
            	// 定义交通灯开启时间的任务
            	task light(color,tics);
            		output color;
            		input [31:0] tics;
            		begin
            			repeat(tics) @(posedge clk);	// 等待tics个时钟上升沿
            			color=off;	// 关灯
            		end
            	endtask
            	
            	// 产生时钟脉冲的always块
            	always begin
            		#100 clk=0;
            		#100 clk=1;
            	end
            endmodule
            ```

          * 注意：

            * 任务定义结构内不允许出现过程块（initial或always过程块）
            * 任务定义结构内可以出现disable终止语句，该语句的执行将中断正在执行的任务。于是程序流将返回到调用任务的地方继续向下执行。

    * function说明语句

      * 函数的目的是返回一个用于表达式的值。函数名蕴含了函数内部的寄存器。

      * 语法：

        ```
        function <返回值的类型或范围>(函数名);
        	<端口说明语句>
        	<变量类型说明语句>
        begin
        	<语句>
        end
        endfunction
        ```

        注意， <返回值的类型或范围>是可选的，缺省则返回1位寄存器类型数据。

      * 示例

        ```
        function [7:0] getbyte;
        	input [15:0] address;
        begin
        	<说明语句> // 从地址字中提取低字节的程序
        	getbyte = result_expression;	// 把结果赋予函数的返回字节
        end
        endfunction
        ```

      * 函数的使用规则

        与任务相比，函数的使用有较多的约束，下面是函数的使用规则：

        * 函数的定义不能包含有任何的时间控制语句，即任何用 #, @, 或 wait 来标识的语句
        * 函数不能启动任务
        * 函数至少要有一个输入参数。
        * 函数定义中必须由一条赋值语句给与函数同名的寄存器赋值。

      * 示例

        ```
        // 统计输入数据中“0”的个数
        function [3:0] out0;
        input [7:0] x;
        reg [3:0] count;
        integer i;
        begin
        	count = 0;
        	for (i = 0; i <= 7; i = i + 1)
        		if (x[i] == 1'b0) count = count + 1;
        	out0 = count;
        end
        endfunction
        ```

      * 注意：

        * 函数不能有任何类型的输出端口和双向端口。
        * 在第一行function语句中不能出现端口名列表。

12. 系统函数和任务

    * Verilog HDL中共有以下一些系统函数和任务

      ```
      $bitstoreal, $rtoi, $display, $setup, $finish, $skew, $hold,
      $setuphold, $itor, $strobe, $period, $time, $printtimescale,
      $timeformat, $realtime, $width, $realtobits, $wirte, $recovery,
      ```

      每个系统函数和任务前面都有一个标识符$。

      对于初学者，系统函数和任务不是首要必须掌握的，可慢慢理解。

    * $display和write任务

      ```
      // 格式
      $display(p1,p2,...,pn);		// 将p2到pn按参数p1给定的格式输出。
      $write(p1,p2,...,pn);		// 同上。区别是$display输出后自动换行，$wrte不会。
      // 几种输出格式
      %h 或 %H, 十六进制
      %d 或 %D, 十进制
      %o 或 %O，八进制
      %b 或 %B, 二进制
      %c 或 %C，ASCII码字符
      %v 或 %V，输出网络型数据信号强度
      %m 或 %M，输出等级层次的名字
      %s 或 %S, 字符串形式
      %t 或 %T，以当前的时间格式输出
      %e 或 %E，以指数的形式输出实型数
      %f 或 %F，以十进制的形式输出实数
      %g 或 %G，以指数或十进制的形式输出实数，以较短的结果输出。
      // 几种转义字符
      \n	换行
      \t	制表符
      \\	反斜杠字符\
      \"	双引号字符"
      \o	1到3位八进制数代表的字符
      %%	百分符号%
      ```

    * $monitor任务

      * 提供了监控和输出参数列表中的表达式或变量值的功能。当启动一个带有一个或多个参数的$monitor任务时，仿真器则建立一个处理机制，使得每当参数列表中变量或表达式的值发生变化时，整个参数列表中变量或表达式的值都将输出显示。

      * 格式

        ```
        $monitor(p1,p2,...,pn);
        $monitor;
        $monitoron;
        $monitoroff;
        ```

      * 示例

        ```
        $monitor($time,,"rxd=%b txd=%b",rxd,txd);
        // 其中，“,,”代表一个空参数。输出时显示为空格。
        ```

    * 时间度量系统函数

      * $time，返回一个64比特的整数表示当前仿真时刻值。该时刻是以模块的仿真时间尺度为基准。
      * $realtime，返回的是一个实型数，改数字也是以时间尺度为基准的。

    * $finish

      * 作用是退出仿真器，返回主操作系统。

    * $stop

      * 作用是把EDA工具（例如仿真器）置成暂停模式。

    * 从文件读取数据到存贮器中。

      ```
      $readmemb
      $readmemh
      // 可指定文件名，存贮器名，起始地址，结束地址
      ```

    * $random

      * 产生随机数

      

13. 编译预处理

    * Verilog HDL编译系统通常先对这些特殊的命令进行“预处理”，然后将预处理的结果和源程序一起在进行通常的编译处理。

    * 预处理命令以“`"开头。这些预处理命令的有效范围为定义命令之后到本文件结束或到其他命令定义替代该命令之处。

    * Verilog提供了以下预编译命令

      ```
      `accelerate, `autoexpand_vectornets, `celldefine, `default_nettype, `define, `else
      `endcelldefine, `endif, `endprotect, `endprotected, `expand_vectornets, `ifdef,
      `include, `noaccelerate, `nounconnected_drive, `protect, `protected, 
      `remove_gatenames, `remove_netnames, `reset, `timescale, `unconnected_drive
      ```

    * 一些说明

      * 引用已定义的宏名时，前面加上符号“`"。
      * 宏定义不是Verilog语句，行末不必加分号。

    * 示例

      ```
      条件编译命令 `ifdef, `else, `endif
      ```

      

### 